buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: "com.android.application"
apply plugin: "eclipse"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.osmdroid:osmdroid-android:4.2'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile 'com.google.code.gson:gson:2.3'
    compile 'ch.acra:acra:4.5.0'
    compile 'com.android.support:appcompat-v7:21.+'
    compile 'com.android.support:support-v4:21.+'
    compile 'com.android.support:gridlayout-v7:21.+'
    compile 'com.android.support:cardview-v7:21.+'
    compile 'it.sephiroth.android.library.horizontallistview:hlistview:1.2.2'
    compile 'com.octo.android.robospice:robospice:1.4.14'
    compile 'com.octo.android.robospice:robospice-cache:1.4.14'
    compile 'com.octo.android.robospice:robospice-google-http-client:1.4.14'
    compile('com.google.http-client:google-http-client-gson:1.19.0') {
        transitive = true
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            aidl.srcDirs = ["src"]
            renderscript.srcDirs = ["src"]
            resources.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
        }
    }

    defaultConfig {
        buildConfigField "String", "LYRK_API_KEY", getLyrkApiKey()
        minSdkVersion 19
        targetSdkVersion 21
    }

    signingConfigs {
        release
    }

    buildTypes {

        release {
            proguardFile "proguard.cfg"
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
        disable "MissingTranslation"
        textReport true
        textOutput 'stdout'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ["-Xlint:all", "-Werror", "-Xlint:-deprecation"]
}

if (hasProperty("StoreFile")) {
    android.signingConfigs.release.storeFile = file(StoreFile)
    android.signingConfigs.release.storePassword = StorePassword
    android.signingConfigs.release.keyAlias = KeyAlias
    android.signingConfigs.release.keyPassword = KeyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

String getLyrkApiKey() {
    if (project.hasProperty("LyrkApiKey")) {
        return "\"" + project["LyrkApiKey"] + "\"";
    }
    return "null";
}


