buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:0.12.2"
    }
}

apply plugin: "com.android.application"
apply plugin: "eclipse"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.osmdroid:osmdroid-android:4.2'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile 'com.google.code.gson:gson:2.3'
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ["src"]
            aidl.srcDirs = ["src"]
            renderscript.srcDirs = ["src"]
            resources.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
        }
    }

    defaultConfig {
        buildConfigField "String", "LYRK_API_KEY", getLyrkApiKey()
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            jniDebugBuild true
        }

        release {
            runProguard true
            proguardFile "proguard.cfg"
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError true
        disable "MissingTranslation"
        textReport true
        textOutput 'stdout'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ["-Xlint:all", "-Werror", "-Xlint:-deprecation"]
}

if (hasProperty("StoreFile")) {
    android.signingConfigs.release.storeFile = file(StoreFile)
    android.signingConfigs.release.storePassword = StorePassword
    android.signingConfigs.release.keyAlias = KeyAlias
    android.signingConfigs.release.keyPassword = KeyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

String getLyrkApiKey() {
    if (project.hasProperty("LyrkApiKey")) {
        return "\"" + project["LyrkApiKey"] + "\"";
    }
    return "null";
}


